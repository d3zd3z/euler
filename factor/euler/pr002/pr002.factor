! Problem 2
!
! 19 October 2001
!
!
! Each new term in the Fibonacci sequence is generated by adding the
! previous two terms. By starting with 1 and 2, the first 10 terms will be:
!
! 1, 2, 3, 5, 8, 13, 21, 34, 55, 89, ...
!
! By considering the terms in the Fibonacci sequence whose values do not
! exceed four million, find the sum of the even-valued terms.
!
! 4613732

USING: kernel io math prettyprint ;
IN: euler.pr002

: next-fib ( a b -- b c )
    swap over + ;

! If 'b' is even? then add it to 'c'.
: gather ( b c -- b c' )
    over even?
    [ over + ] when ;

!
: fib-step ( a b c -- a' b' c' )
    gather
    [ next-fib ] dip ;

: fib-loop ( a b c -- a' b' c' )
    over  4000000 <
    [ fib-step  fib-loop ] when ;

: solve002 ( -- n )
    1 2 0
    fib-loop
    2nip ;
MAIN: solve002
