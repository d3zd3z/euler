% Problem 12
%
% 08 March 2002
%
%
% The sequence of triangle numbers is generated by adding the natural
% numbers. So the 7^th triangle number would be 1 + 2 + 3 + 4 + 5 + 6 + 7 =
% 28. The first ten terms would be:
%
% 1, 3, 6, 10, 15, 21, 28, 36, 45, 55, ...
%
% Let us list the factors of the first seven triangle numbers:
%
%      1: 1
%      3: 1,3
%      6: 1,2,3,6
%     10: 1,2,5,10
%     15: 1,3,5,15
%     21: 1,3,7,21
%     28: 1,2,4,7,14,28
%
% We can see that 28 is the first triangle number to have over five
% divisors.
%
% What is the value of the first triangle number to have over five hundred
% divisors?
%
% 76576500

-module(pr012).
-export([solve/0]).

solve() ->
	S = sieve:start(),
	Answer = solve(S, 1, 1),
	sieve:stop(S),
	Answer.

solve(S, N, Tri) ->
	Count = divisor_count(S, Tri),
	if
		Count > 500 ->
			Tri;
		true ->
			N2 = N + 1,
			solve(S, N2, Tri + N2)
	end.

% Determine the number of divisors for a given number.
divisor_count(S, N) -> divisor_count(S, N, 2, 1).

divisor_count(_S, 1, _P, Res) -> Res;
divisor_count(S, N, P, Res) when N rem P =:= 0 ->
	{N2, Count} = multi_divide(N, P, 0),
	P2 = sieve:next_prime(S, P),
	divisor_count(S, N2, P2, Res * (Count + 1));
divisor_count(S, N, P, Res) ->
	P2 = sieve:next_prime(S, P),
	divisor_count(S, N, P2, Res).

multi_divide(N, P, Count) when N rem P =:= 0 ->
	multi_divide(N div P, P, Count + 1);
multi_divide(N, _P, Count) -> {N, Count}.
