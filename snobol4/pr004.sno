* vim: ft=snobol4
* Problem 4
*
* 16 November 2001
*
* A palindromic number reads the same both ways. The largest palindrome made
* from the product of two 2-digit numbers is 9009 = 91 Ã— 99.
*
* Find the largest palindrome made from the product of two 3-digit numbers.
*
* 906609

	define('pal(num)')
	define('revnum(n)')

* Outer loop
	a = 100
outloop
	le(a,999)		:f(outstop)

	b = a
inloop
	le(b,999)		:f(instop)

	tmp = a * b
	pal(tmp)		:f(notpal)

	biggest = gt(tmp,biggest) tmp
notpal

	b = b + 1		:(inloop)
instop
	a = a + 1		:(outloop)
outstop

	output = biggest

	:(end)

* Reverse the digits of a number.
revnum	revnum = 0
revnum.loop
	revnum = gt(n,0) revnum * 10 + remdr(n,10)	:f(return)
	n = n / 10					:(revnum.loop)

* Determine if the given number is a palindrome
pal	eq(num,revnum(num))	:f(freturn)s(return)

end
