----------------------------------------------------------------------
--  Problem 44

--
--  23 May 2003
--
--
--  Pentagonal numbers are generated by the formula, P[n]=n(3n−1)/2. The
--  first ten pentagonal numbers are:
--
--  1, 5, 12, 22, 35, 51, 70, 92, 117, 145, ...
--
--  It can be seen that P[4] + P[7] = 22 + 70 = 92 = P[8]. However, their
--  difference, 70 − 22 = 48, is not pentagonal.
--
--  Find the pair of pentagonal numbers, P[j] and P[k], for which their
--  sum and difference is pentagonal and D = |P[k] − P[j]| is minimised;
--  what is the value of D?
--
--
--
----------------------------------------------------------------------

with Ada.Text_IO; use Ada.Text_IO;
with Euler; use Euler;

procedure Pr044 is

   function Is_Pentagonal (Number : Natural) return Boolean;
   function Nth_Pentagonal (Index : Natural) return Natural;

   function Find return Natural;

   function Find return Natural is
      I : Natural := 2;
      Pent_I, Pent_J : Natural;
   begin
      loop
         Pent_I := Nth_Pentagonal (I);

         for J in 1 .. I - 1 loop
            Pent_J := Nth_Pentagonal (J);

            if Is_Pentagonal (Pent_I - Pent_J) and then
               Is_Pentagonal (Pent_I + Pent_J)
            then
               return Pent_I - Pent_J;
            end if;
         end loop;

         I := I + 1;
      end loop;
   end Find;

   -------------------
   -- Is_Pentagonal --
   -------------------

   function Is_Pentagonal (Number : Natural) return Boolean is
      Sqr : constant Natural := 1 + Number * 24;
      Root : constant Natural := ISqrt (Sqr);
   begin
      return (Root * Root) = Sqr and then
         (Root + 1) mod 6 = 0;
   end Is_Pentagonal;

   --------------------
   -- Nth_Pentagonal --
   --------------------

   function Nth_Pentagonal (Index : Natural) return Natural is
   begin
      return Index * (3 * Index - 1) / 2;
   end Nth_Pentagonal;

begin
   Put_Line (Natural'Image (Find));
end Pr044;

