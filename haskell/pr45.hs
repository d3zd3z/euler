----------------------------------------------------------------------
-- Triangle, pentagonal, and hexagonal numbers are generated by the
-- following formulae:
-- Triangle             T_(n)=n(n+1)/2          1, 3, 6, 10, 15, ...
-- Pentagonal           P_(n)=n(3n−1)/2         1, 5, 12, 22, 35, ...
-- Hexagonal            H_(n)=n(2n−1)           1, 6, 15, 28, 45, ...
--
-- It can be verified that T_(285) = P_(165) = H_(143) = 40755.
--
-- Find the next triangle number that is also pentagonal and
-- hexagonal.
----------------------------------------------------------------------

module Main where

main :: IO ()
main = print $ (shared triangle pentagonal hexagonal) !! 2

triangle, pentagonal, hexagonal :: [Int]

triangle = [ n*(n+1) `div` 2 | n <- [1..] ]
pentagonal = [ n*(3*n-1) `div` 2 | n <- [1..] ]
hexagonal = [ n*(2*n-1) | n <- [1..] ]

shared :: [Int] -> [Int] -> [Int] -> [Int]
shared (a:as) (b:bs) (c:cs)
   | a == b && b == c   = a : shared as bs cs
   | a <= b && a <= c     = shared as (b:bs) (c:cs)
   | b <= a && b <= c     = shared (a:as) bs (c:cs)
   | otherwise          = shared (a:as) (b:bs) cs
shared _ _ _ = error "Out of infinite numbers"
