(*
 * Problem 44
 *
 * 23 May 2003
 *
 * Pentagonal numbers are generated by the formula, P[n]=n(3n−1)/2. The
 * first ten pentagonal numbers are:
 *
 * 1, 5, 12, 22, 35, 51, 70, 92, 117, 145, ...
 *
 * It can be seen that P[4] + P[7] = 22 + 70 = 92 = P[8]. However,
 * their difference, 70 − 22 = 48, is not pentagonal.
 *
 * Find the pair of pentagonal numbers, P[j] and P[k], for which their
 * sum and difference is pentagonal and D = |P[k] − P[j]| is minimised;
 * what is the value of D?
 *
 * 5482660
 *)

open! Core.Std

let nth_pentagonal n =
  (n * (n * 3 - 1)) / 2

let is_pentagonal n =
  let sq = 24 * n + 1 in
  let root = Misc.isqrt sq in
  (root * root = sq) && ((root + 1) mod 6) = 0

let solve () =
  let rec outer i =
    let penti = nth_pentagonal i in

    let rec inner j =
      if j = i then
        outer (i + 1)
      else begin
        let pentj = nth_pentagonal j in

        if is_pentagonal (penti - pentj) && is_pentagonal (penti + pentj) then
          penti - pentj
        else
          inner (j + 1)
      end
    in inner 1
  in outer 2

let run () =
  printf "%d\n" (solve ())
