\ Problem 2
\ vim: ft=forth
\
\ 19 October 2001
\
\
\ Each new term in the Fibonacci sequence is generated by adding the
\ previous two terms. By starting with 1 and 2, the first 10 terms will be:
\
\ 1, 2, 3, 5, 8, 13, 21, 34, 55, 89, ...
\
\ By considering the terms in the Fibonacci sequence whose values do not
\ exceed four million, find the sum of the even-valued terms.
\
\ 4613732

: even ( a -- f )
   1 and 0= ;

\ Given an accumulator, and two steps in a fibonacci number, add the latest
\ fibonacci to the accumulator if it is even.
: gather ( a b c -- a2 b c )
   dup even if  rot over + -rot  then ;

: fib-next ( a b -- b c )  \ Advance to the next fibonacci number
   swap over + ;

: solve ( -- n )
   0  1 1
   begin
      dup  4000000 <
   while
      gather
      fib-next
   repeat
   2drop ;

\ solve .
