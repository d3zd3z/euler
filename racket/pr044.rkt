#lang racket

;;; Problem 44
;;;
;;; 23 May 2003
;;;
;;; Pentagonal numbers are generated by the formula, P[n]=n(3n−1)/2. The first
;;; ten pentagonal numbers are:
;;;
;;; 1, 5, 12, 22, 35, 51, 70, 92, 117, 145, ...
;;;
;;; It can be seen that P[4] + P[7] = 22 + 70 = 92 = P[8]. However, their
;;; difference, 70 − 22 = 48, is not pentagonal.
;;;
;;; Find the pair of pentagonal numbers, P[j] and P[k], for which their sum
;;; and difference is pentagonal and D = |P[k] − P[j]| is minimised; what is
;;; the value of D?

(define (pentagonal? number)
  (define sq (add1 (* number 24)))
  (define root (integer-sqrt sq))
  (and (= (expt root 2) sq)
       (zero? (remainder (add1 root) 6))))

(define (nth-pentagonal n)
  (/ (* n (sub1 (* n 3)))
     2))

(define (euler-44)
  (let/ec return
    (for ([i (in-naturals 2)])
      (define pent-i (nth-pentagonal i))
      (for ([j (in-range 1 i)])
        (define pent-j (nth-pentagonal j))
        (and (pentagonal? (- pent-i pent-j))
             (pentagonal? (+ pent-i pent-j))
             (return (- pent-i pent-j)))))))
