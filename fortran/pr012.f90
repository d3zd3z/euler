! Problem 12
!
! 08 March 2002
!
!
! The sequence of triangle numbers is generated by adding the natural
! numbers. So the 7^th triangle number would be 1 + 2 + 3 + 4 + 5 + 6 +
! 7 = 28. The first ten terms would be:
!
! 1, 3, 6, 10, 15, 21, 28, 36, 45, 55, ...
!
! Let us list the factors of the first seven triangle numbers:
!
!      1: 1
!      3: 1,3
!      6: 1,2,3,6
!     10: 1,2,5,10
!     15: 1,3,5,15
!     21: 1,3,7,21
!     28: 1,2,4,7,14,28
!
! We can see that 28 is the first triangle number to have over five
! divisors.
!
! What is the value of the first triangle number to have over five
! hundred divisors?
! 76576500

subroutine pr012

  use primes
  implicit none
  integer tri, n

  n = 1
  tri = 1
  do
    if (divisor_count(tri) > 500) then
      print *, tri
      exit
    end if
    n = n + 1
    tri = tri + n
  end do

contains

  function divisor_count (n)
    implicit none

    integer, intent(in) :: n
    integer :: divisor_count
    integer :: tmp, prime_index, prime, divide_count

    divisor_count = 1
    tmp = n
    prime_index = 1
    prime = 2

    do while (tmp > 1)
      divide_count = 0
      do while (mod (tmp, prime) == 0)
        tmp = tmp / prime
        divide_count = divide_count + 1
      end do

      divisor_count = divisor_count * (divide_count + 1)

      if (tmp == 1) exit

      prime_index = prime_index + 1
      prime = nth_prime (prime_index)
    end do
  end function

end subroutine pr012
