/**********************************************************************
 * Problem 9
 *
 * 25 January 2002
 *
 *
 * A Pythagorean triplet is a set of three natural numbers, a < b < c, for
 * which,
 *
 * a^2 + b^2 = c^2
 *
 * For example, 3^2 + 4^2 = 9 + 16 = 25 = 5^2.
 *
 * There exists exactly one Pythagorean triplet for which a + b + c = 1000.
 * Find the product abc.
 **********************************************************************/

/* 31875000 */

Module "Pr009"
Uses "Avail"
Entries "Solve"
Body

Method "scan" is
[
	$done : integer;
	From 1 to 999 do [
		a : [1..999]
	|
		From a to 999 do [
			b : [1..999]
		|
			c ::= 1000 - a - b;
			If c > b ∧ [a×a + b×b = c×c] then [
				Exit done with a×b×c
				/* Print: “a×b×c”; */
			];
		];
	];
	0
] : integer;

Method "Solve" is
[
	answer ::= scan;
	Print: “answer”;
];
