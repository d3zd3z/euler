/**********************************************************************
 * Problem 4
 *
 * 16 November 2001
 *
 *
 * A palindromic number reads the same both ways. The largest palindrome made
 * from the product of two 2-digit numbers is 9009 = 91 × 99.
 *
 * Find the largest palindrome made from the product of two 3-digit numbers.
**********************************************************************/

Module "Pr004"
Uses "Avail"
Entries "Solve"
Body

_shortNumber ::= [0 .. 999999];
/* _shortNumber ::= whole number; */

Private method "short number" is [_shortNumber];

Method "reverse_'s digits" is
[
	number : short number
|
	work : short number := number;
	result : short number := 0;
	While [work > 0] do [
		result := cast result × 10 + work mod 10 into [ t : short number | t ];
		work := work ÷ 10;
	];
	result
] : short number;

Method "_is a palindrome" is
[
	value : short number
|
	value = reverse value's digits
] : boolean;

Method "Solve" is
[
	answer : short number := 0;

	From 100 to 999 do [
		a : [100..999]
	|
		/* Print: format "a = “①”\n" with a; */
		From a to 999 do [
			b : [100..999]
		|
			c ::= a × b;
			If c > answer ∧ [c is a palindrome] then [
				answer := c;
			];
		];
	];

        Print: “answer”;
];
