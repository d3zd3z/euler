#lang scheme

;;; Each new term in the Fibonacci sequence is generated by adding the
;;; previous two terms. By starting with 1 and 2, the first 10 terms
;;; will be:
;;; 
;;; 1, 2, 3, 5, 8, 13, 21, 34, 55, 89, ...
;;; 
;;; Find the sum of all the even-valued terms in the sequence which do
;;; not exceed four million.

(require scheme/generator)

(define (make-fibs)
  (generator
    (let loop ([a 1] [b 1])
      (yield a)
      (loop b (+ a b)))))

(define (compute limit)
  (for/fold ([sum 0])
	    ([fib (in-producer (make-fibs) (lambda (x) (>= x limit)))]
	     #:when (even? fib))
	    (+ sum fib)))

(display (compute 4000000))
(newline)
