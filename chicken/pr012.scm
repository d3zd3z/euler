;;; Problem 12
;;;
;;; 08 March 2002
;;;
;;; The sequence of triangle numbers is generated by adding the natural
;;; numbers. So the 7^th triangle number would be 1 + 2 + 3 + 4 + 5 + 6 + 7 =
;;; 28. The first ten terms would be:
;;;
;;; 1, 3, 6, 10, 15, 21, 28, 36, 45, 55, ...
;;;
;;; Let us list the factors of the first seven triangle numbers:
;;;
;;;      1: 1
;;;      3: 1,3
;;;      6: 1,2,3,6
;;;     10: 1,2,5,10
;;;     15: 1,3,5,15
;;;     21: 1,3,7,21
;;;     28: 1,2,4,7,14,28
;;;
;;; We can see that 28 is the first triangle number to have over five
;;; divisors.
;;;
;;; What is the value of the first triangle number to have over five hundred
;;; divisors?
;;;
;;; 76576500

(declare (uses sieve))
(import sieve)

(define (divisor-count sieve n)
  (let loop ((result 1)
	     (n n)
	     (prime 2))
    (if (= n 1) result
      (let iloop ((divide-count 0)
		  (n n))
	(if (zero? (remainder n prime))
	  (iloop (add1 divide-count)
		 (quotient n prime))
	  (loop (* result (add1 divide-count))
		n
		(next-prime sieve prime)))))))

(define (euler-12)
  (define sieve (make-sieve))
  (let loop ((n 1)
	     (tri 1))
    (if (> (divisor-count sieve tri) 500)
      tri
      (loop (add1 n) (+ tri n 1)))))

(display (time (euler-12)))
(newline)
